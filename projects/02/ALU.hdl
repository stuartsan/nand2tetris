// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    // Zero out x if zx==1, else pass through x
    Mux16(a=x, b=false, sel=zx, out=outx1);
    
    // Negate x if nx==1, else pass through x
    Not16(in=outx1, out=notx);
    Mux16(a=outx1, b=notx, sel=nx, out=outx2);
    
    // Zero out y if zy==1, else pass through y
    Mux16(a=y, b=false, sel=zy, out=outy1);
    
    // Negate y if ny==1, else pass through y
    Not16(in=outy1, out=noty);
    Mux16(a=outy1, b=noty, sel=ny, out=outy2);
 
    //If f==1 out = x PLUS y, else out = x AND y
    Add16(a=outx2, b=outy2, out=xplusy);
    And16(a=outx2, b=outy2, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=out1);

    //If no==1 negate output, if output<0 set ng true, else false
    Not16(in=out1, out=notout);
    Mux16(a=out1, b=notout, sel=no, out=out, out=out3, out[15]=ng);

    //If output==0 set zr to true, else false. Need to take a multi-bit
    //input and return a single bit output so use multi-way Ors. Had to 
    //build the Or16Way.
    Or16Way(in=out3, out=or1);
    Not(in=or1, out=zr);

}